// Copyright 2019 the V8 project authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

#include 'src/builtins/builtins-typed-array-gen.h'

namespace typed_array_foreach {
  const kBuiltinName: constexpr string = '%TypedArray%.prototype.forEach';

  transitioning macro ForEachAllElements(implicit context: Context)(
      array: typed_array::AttachedJSTypedArray, callbackfn: Callable,
      thisArg: Object): Object {
    let witness = typed_array::NewAttachedJSTypedArrayWitness(array);
    const length: Smi = Convert<Smi>(array.length);
    for (let k: Smi = 0; k < length; k++) {
      // BUG(4895): We should throw on detached buffers rather than simply exit.
      witness.Recheck() otherwise break;
      const value: Object = witness.Load(k);
      Call(context, callbackfn, thisArg, value, k, witness.GetStable());
    }
    return Undefined;
  }

  // https://tc39.github.io/ecma262/#sec-%typedarray%.prototype.every
  transitioning javascript builtin
  TypedArrayPrototypeForEach(implicit context: Context)(
      receiver: Object, ...arguments): Object {
    // arguments[0] = callback
    // arguments[1] = this_arg.

    try {
      const array: JSTypedArray = Cast<JSTypedArray>(receiver)
          otherwise NotTypedArray;
      const uarray = typed_array::EnsureAttached(array) otherwise IsDetached;

      const callbackfn = Cast<Callable>(arguments[0]) otherwise NotCallable;
      const thisArg = arguments[1];
      return ForEachAllElements(uarray, callbackfn, thisArg);
    }
    label NotCallable deferred {
      ThrowTypeError(kCalledNonCallable, arguments[0]);
    }
    label NotTypedArray deferred {
      ThrowTypeError(kNotTypedArray, kBuiltinName);
    }
    label IsDetached deferred {
      ThrowTypeError(kDetachedOperation, kBuiltinName);
    }
  }
}

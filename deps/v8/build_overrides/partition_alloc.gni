# Copyright 2025 The V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Import into partition_alloc, some required variable definitions.
import("//build/config/compiler/compiler.gni")
import("//build/config/dcheck_always_on.gni")
import("//build/config/sanitizers/sanitizers.gni")

# partition_alloc is performance critical and it should generally be optimized
# for speed, even in debug mode.
partition_alloc_remove_configs =
    [ "//build/config/compiler:default_optimization" ]
partition_alloc_add_configs = [ "//build/config/compiler:optimize_speed" ]

partition_alloc_enable_arc_config = "//build/config/compiler:enable_arc"

# MSVC's cl.exe compiler is not supported.
_supported_compiler = is_clang || !is_win

# Windows: debug CRT is not yet compatible with the allocator shim.
# https://learn.microsoft.com/en-us/cpp/c-runtime-library/reference/free-dbg
_supported_build_type = !is_win || (!is_component_build && !is_debug)

_supported_platform =
    is_android || is_chromeos || is_fuchsia || is_linux || is_mac || is_win

# Sanitizers are already replacing the allocator with their own. We shouldn't
# enable partition_alloc when this happens
_use_sanitizer = is_asan || is_cfi || is_msan || is_tsan || is_ubsan

_supported = _supported_compiler && _supported_build_type &&
             _supported_platform && !_use_sanitizer

use_partition_alloc_as_malloc_default = _supported
use_allocator_shim_default = _supported
enable_backup_ref_ptr_support_default = _supported
put_ref_count_in_previous_slot_default = true
enable_backup_ref_ptr_slow_checks_default = false
enable_dangling_raw_ptr_checks_default = _supported
assert_cpp20_default = true

# V8 doesn't use raw_ptr, so it doesn't really matter. Still, it has to be
# defined, so we take the configuration used by v8.
raw_ptr_zero_on_construct_default = true
raw_ptr_zero_on_move_default = true
raw_ptr_zero_on_destruct_default = false

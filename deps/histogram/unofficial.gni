# This file is used by GN for building, which is NOT the build system used for
# building official binaries.
# Please edit the gyp files if you are making changes to build system.

# The actual configurations are put inside a template in unofficial.gni to
# prevent accidental edits from contributors.
template("histogram_gn_build") {
  config("histogram_config") {
    include_dirs = [ "include" ]
  }

  gypi_values = exec_script("../../tools/gypi_to_gn.py",
                            [ rebase_path("histogram.gyp") ],
                            "scope",
                            [ "histogram.gyp" ])

  source_set(target_name) {
    forward_variables_from(invoker, "*")
    public_configs = [ ":histogram_config" ]
    sources = gypi_values.histogram_sources
    if (is_clang || !is_win) {
      cflags_c = [
        "-Wno-atomic-alignment",
        "-Wno-incompatible-pointer-types",
        "-Wno-unused-function",
      ]
    }
    if (is_linux) {
      libs = [ "atomic" ]
    }
  }
}

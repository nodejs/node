# This file is used by GN for building, which is NOT the build system used for
# building official binaries.
# Please edit the gyp files if you are making changes to build system.

# The actual configurations are put inside a template in unofficial.gni to
# prevent accidental edits from contributors.
template("uvwasi_gn_build") {
  config("uvwasi_config") {
    include_dirs = [ "include" ]
  }

  gypi_values = exec_script("../../tools/gypi_to_gn.py",
                            [ rebase_path("uvwasi.gyp") ],
                            "scope",
                            [ "uvwasi.gyp" ])

  source_set(target_name) {
    forward_variables_from(invoker, "*")

    public_configs = [ ":uvwasi_config" ]
    sources = gypi_values.uvwasi_sources
    include_dirs = [ "src" ]
    deps = [ "../uv" ]

    if (is_clang || !is_win) {
      cflags_c = [
        "-Wno-extra-semi",
        "-Wno-shadow",
      ]
    }
  }
}
